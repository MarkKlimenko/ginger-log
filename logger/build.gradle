plugins {
    id "org.jetbrains.kotlin.jvm" version "$kotlinVersion"
    id "org.jetbrains.kotlin.plugin.spring" version "$kotlinVersion"
    id "org.jetbrains.kotlin.kapt" version "$kotlinVersion"

    id "com.markklim.popcorn.quality" version "$codeQualityVersion"

    id "java-library"
    id "maven-publish"
    id "signing"
}

group = libraryGroup
archivesBaseName = libraryArchivesBaseName
version = libraryVersion

java {
    withSourcesJar()
    withJavadocJar()
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    implementation "io.projectreactor.kotlin:reactor-kotlin-extensions:$kotlinReactorVersion"

    implementation "org.springframework.boot:spring-boot-starter-webflux:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-starter-validation:$springBootVersion"

    compileOnly "com.playtika.reactivefeign:feign-reactor-webclient:$feignReactorVersion"
    compileOnly "com.playtika.reactivefeign:feign-reactor-cloud:$feignReactorVersion"
    compileOnly "com.playtika.reactivefeign:feign-reactor-spring-configuration:$feignReactorVersion"

    implementation "com.github.ben-manes.caffeine:caffeine:$caffeineVersion"

    kapt "org.springframework.boot:spring-boot-configuration-processor:$springBootVersion"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = libraryGroup
            artifactId = libraryArchivesBaseName
            version = libraryVersion

            from components.java

            pom {
                name = 'Spring extended logger - Ginger'
                description = 'Spring request/response logging library'
                url = 'https://github.com/MarkKlimenko/ginger-log'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'markklim'
                        name = 'Mark Klimenko'
                        email = 'mark.klimenko.w@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/MarkKlimenko/ginger-log.git'
                    developerConnection = 'scm:git:ssh://github.com/MarkKlimenko/ginger-log.git'
                    url = 'https://github.com/MarkKlimenko/ginger-log'
                }
            }
        }
    }
    repositories {
        maven {
            name "nexus"
            url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials(PasswordCredentials)
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}