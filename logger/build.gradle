buildscript {
    ext {
        coverageExclusion = [
                '**/**',
        ]

        // library properties
        libraryGroup = 'com.markklim.libs'
        libraryArchivesBaseName = "ginger-log"
        libraryVersion = "1.1.1"

        // versions
        kotlinVersion = "1.7.22"
        kotlinLoggingVersion = "2.0.11"

        springBootVersion = "2.7.9"

        junitVersion = "5.8.1"
        codeQualityVersion = "1.1"
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "$kotlinVersion"
    id "org.jetbrains.kotlin.plugin.spring" version "$kotlinVersion"
    id "org.jetbrains.kotlin.kapt" version "$kotlinVersion"

    id "com.markklim.popcorn.quality" version "$codeQualityVersion"

    id 'java-library'
    id "maven-publish"
    id "signing"
}

group = libraryGroup
archivesBaseName = libraryArchivesBaseName
version = libraryVersion

java {
    withSourcesJar()
    withJavadocJar()
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    compileOnly "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.1"
    compileOnly "org.jetbrains.kotlinx:kotlinx-coroutines-reactive:1.7.1"
    compileOnly "org.jetbrains.kotlinx:kotlinx-coroutines-reactor:1.7.1"
    compileOnly "io.github.microutils:kotlin-logging-jvm:$kotlinLoggingVersion"

    compileOnly "org.springframework.boot:spring-boot-starter-webflux:$springBootVersion"

    kapt "org.springframework.boot:spring-boot-configuration-processor:$springBootVersion"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = libraryGroup
            artifactId = libraryArchivesBaseName
            version = libraryVersion

            from components.java

            pom {
                name = 'Spring extended logger - Ginger'
                description = 'Spring request/response logging library'
                url = 'https://github.com/MarkKlimenko/ginger-log'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'markklim'
                        name = 'Mark Klimenko'
                        email = 'mark.klimenko.w@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/MarkKlimenko/ginger-log.git'
                    developerConnection = 'scm:git:ssh://github.com/MarkKlimenko/ginger-log.git'
                    url = 'https://github.com/MarkKlimenko/ginger-log'
                }
            }
        }
    }
    repositories {
        maven {
            name "nexus"
            url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials(PasswordCredentials)
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

test {
    useJUnitPlatform()
}